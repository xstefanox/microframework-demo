buildscript {
    ext.kotlinVersion = '1.2.41'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.41' apply false
    id 'com.github.johnrengelman.shadow' version '2.0.4' apply false
}

group 'io.github.xstefanox'
version '1.0-SNAPSHOT'

apply plugin: 'idea'

subprojects {

    apply plugin: 'kotlin'

    test {
        useJUnitPlatform()
    }

    repositories {
        jcenter()
    }

    dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlinVersion
        implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
        implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.5.4'
        implementation group: 'com.typesafe', name: 'config', version: '1.3.3'
        implementation group: 'org.kodein.di', name: 'kodein-di-generic-jvm', version: '5.0.0'

        testImplementation group: 'io.kotlintest', name: 'kotlintest-runner-junit5', version: '3.1.6'
        testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '3.1.0'
    }

    compileKotlin {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.1"
            languageVersion = "1.1"
        }
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}

project(':core') {

}

project(':rest') {
    dependencies {
        compile project(':core')
        implementation group: 'io.undertow', name: 'undertow-core', version: '2.0.9.Final'
    }
}

project(':backend') {
    dependencies {
        compile project(':core')
        implementation group: 'org.apache.camel', name: 'camel-core', version: '2.21.1'
    }
}

project(':cli') {

    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'io.github.xstefanox.demo.mf.cli.Main'

    dependencies {
        compile project(':core')
        implementation group: 'com.github.ajalt', name: 'clikt', version: '1.2.0'
    }
}

project(':service') {

    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'io.github.xstefanox.demo.mf.Main'

    dependencies {
        compile project(':backend')
        compile project(':rest')
    }
}

task shadow {
    dependsOn("service:shadowJar")
    dependsOn("cli:shadowJar")
}
