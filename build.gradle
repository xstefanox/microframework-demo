buildscript {
    ext.kotlinVersion = '1.3.20'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.20' apply false
    id 'com.github.johnrengelman.shadow' version '4.0.4' apply false
    id 'org.jlleitschuh.gradle.ktlint' version '6.3.1' apply false
}

group 'io.github.xstefanox'
version '1.0-SNAPSHOT'

apply plugin: 'idea'

subprojects {

    apply plugin: 'kotlin'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    test {
        useJUnitPlatform()
    }

    repositories {
        jcenter()
    }

    dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlinVersion
        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.1.1'
        implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
        implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.6.22'
        implementation group: 'com.typesafe', name: 'config', version: '1.3.3'
        implementation group: 'org.kodein.di', name: 'kodein-di-generic-jvm', version: '6.0.1'

        testImplementation group: 'io.kotlintest', name: 'kotlintest-runner-junit5', version: '3.2.1'
    }

    compileKotlin {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.3"
            languageVersion = "1.3"
        }
    }

    compileTestKotlin {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.3"
            languageVersion = "1.3"
        }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
    
    ktlint {
        outputToConsole = true
    }
}

project(':core') {

}

project(':rest') {
    dependencies {
        implementation project(':core')
        implementation group: 'io.undertow', name: 'undertow-core', version: '2.0.16.Final'

        testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'
    }
}

project(':backend') {
    dependencies {
        implementation project(':core')
        implementation group: 'org.apache.camel', name: 'camel-core', version: '2.23.1'
    }
}

project(':cli') {

    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'io.github.xstefanox.demo.mf.cli.Main'

    dependencies {
        implementation project(':core')
        implementation group: 'com.github.ajalt', name: 'clikt', version: '1.6.0'
    }
}

project(':service') {

    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'io.github.xstefanox.demo.mf.Main'

    dependencies {
        implementation project(':core')
        implementation project(':backend')
        implementation project(':rest')

        testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'
    }
}

task shadow {
    dependsOn("service:shadowJar")
    dependsOn("cli:shadowJar")
}
